plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.3'
}

group = 'controllers'
description = 'A desktop Widget to display Pi-hole data'

repositories {
    mavenCentral()
}

def junitVersion = '5.10.2'
def javaRelease = 25

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaRelease)
    }
    // Ensure all dependencies are available as modules
    modularity.inferModulePath = true
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = javaRelease
    options.compilerArgs += [
        '-Xlint:deprecation',
        '-Xlint:unchecked'
    ]
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'pihole'
    mainClass = 'controllers.WidgetApplication'
    applicationDefaultJvmArgs = [
        '--enable-native-access=javafx.base,javafx.graphics,javafx.controls,javafx.fxml,javafx.web,javafx.swing'
    ]
}

// Pass pihole.verbose property to the running application
tasks.named('run') {
    def verboseFlag = project.findProperty('pihole.verbose') ?: 'false'
    jvmArgs += ["-Dpihole.verbose=${verboseFlag}"]
}

javafx {
    version = '25-ea+21'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.2.2')
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('net.synedra:validatorfx:0.6.1') {
        exclude(group: 'org.openjfx')
    }
    implementation('org.kordamp.ikonli:ikonli-javafx:12.4.0')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation('eu.hansolo:tilesfx:21.0.9') {
        exclude(group: 'org.openjfx')
    }

    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    implementation 'org.jetbrains:annotations:26.0.2'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.10.2")
}

test {
    useJUnitPlatform()
}

jlink {

    mergedModule {
        requires "java.xml"
    }

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'Pihole Widget'
        jvmArgs = [
            '--enable-native-access=javafx.base,javafx.graphics,javafx.controls,javafx.fxml,javafx.web,javafx.swing'
        ]
    }

    version = "1.5.2"
    description ="A desktop Widget to display Pihole data"


    jpackage {
        // jpackageHome = '/usr/lib/jvm/jdk16'
        // outputDir = 'my-packaging'
        // imageOutputDir = file("$buildDir/my-packaging-image")
        // installerOutputDir = file("$buildDir/my-packaging-installer")
        // imageName = 'MyApp'
        //imageOptions = ['--win-console']
        skipInstaller = false
        installerName = 'Widget Installer'
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyright (C) 2022.  Reda ELFARISSI aka foxy999'
        ]
        
        // Initialize imageOptions with JVM arguments for native access to JavaFX modules
        imageOptions = [
            '--java-options', '--enable-native-access=javafx.base,javafx.graphics,javafx.controls,javafx.fxml,javafx.web,javafx.swing'
        ]
        
        //if (org.gradle.internal.os.OperatingSystem.current().windows) {
        if (installerType == 'msi') {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--icon','src/main/resources/media/icons/icon.ico']
        }
        if (installerType == 'pkg') {
            //imageOptions += ['--icon', 'src/main/resources/..../icon.icns']
        }
        if (installerType in ['deb', 'rpm']) {
            //imageOptions += ['--icon', 'src/main/resources/..../icon_256x256.png']
            installerOptions += [
                    '--linux-menu-group', 'Office',
                    '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                    '--linux-deb-maintainer', 'fox-inflames@outlook.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                    '--linux-rpm-license-type', 'GPLv3'
            ]
        }

    }

}

def JLINK_DIR = "$buildDir/jpackage/Pihole Widget"
/*
tasks.jlink.doLast {

    copy {
        from("/") {
            include "README.md","CHANGELOG.md"
        }
        into JLINK_DIR+"/docs"
    }
    copy {
        from("/src/main/resources/media/images") {

        }
        into JLINK_DIR+"/media/images"
    }

}
*/
jlinkZip {
    group = 'distribution'
}
jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}